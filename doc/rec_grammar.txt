SS2 =
    { ( ruleset | directive ) }
.
QuotedString =
    ( "'" {ANY} "'" | '"' {ANY} '"' )
.
URI =
    "url" [ "(" ] ( QuotedString | {ANY} ) [ ")" ]
.
medium =
    (
        "all" | "aural" | "braille" | "embossed"
        | "handheld" | "print" | "projection"
        | "screen" | "tty" | "tv"
    )
.
identity =
    (
        ident
        | "n" | "url" | "all" | "aural" | "braille"
        | "embossed" | "handheld" | "print"
        | "projection" | "screen" | "tty" | "tv"
    )
.
directive =
    '@' identity
    [ expr | medium ]
    (
        '{' [ {
            (
              declaration { ';' declaration } [ ';' ]

            | ruleset
            | directive
            )
        } ] '}'
    |
        ';'
    )
.
ruleset =
    selector
    { ',' selector }
    '{' [ declaration { ';' declaration } [ ';' ] ] '}'

.
selector =
    simpleselector { [ ( '+' | '>' | '~' ) ] simpleselector }

.
simpleselector =
    ( identity | '*'
    | ('#' identity | '.' identity | attrib | pseudo )

    )
    { ('#' identity | '.' identity | attrib | pseudo ) }
.
attrib =

    '[' identity [
        ( '=' | "~=" | "|=" | "$=" | "^=" | "*=" )
        ( identity | QuotedString )

    ] ']'
.
pseudo =
    ':' [ ':' ] identity [ '(' expr ')' ]

.
declaration =
    identity ':' expr [ "!important" ]
.
expr =
    term { [ ( '/' | ',' ) ] term }

.
term =
    (
      QuotedString
    | URI
    | "U\\" identity
    | HexValue
    | identity
        [ { (
            ':' [ ':' ] identity
          | '.' identity
          | '=' ( identity | { digit } )
        ) } ]

        [ '(' expr ')' ]
    |
        [ ( '-' | '+' ) ]

        { digit }
        [ '.' { digit } ]
        [ (
              "n" [ ( "+" | "-" ) digit { digit } ]

            | "%"

            | identity
        ) ]
    )
.
HexValue =
    '#'

    [ { digit } ]
    [ ident ]